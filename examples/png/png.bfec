
#--
PNG Image Format
https://www.w3.org/TR/PNG
#--
struct $ {
  # Magic bytes to identify the PNG file format
  magic_number: 0x8950_4e47_0d0a_1a0a;
  
  # Image header should always be the first chunk, and we need to reference data out
  # of it, so we'll statically define it here so we can `$.image_header` elsewhere
  image_header: Chunk(ChunkType.image_header);

  chunks: Chunk[...];
}

struct Chunk (type: ChunkType) {
  length: len<u32_be>;
  type: ChunkType = type;
  data: u8[@.length] -> ChunkData(type);
  crc: checksum<u32_be>(@.data, 'crc32');
}

# TODO: Add support for string enums
# TODO: Add support for `<len>` syntax for string lengths (to differentiate from arrays)
enum ChunkType: ascii<4> {
  image_header              = "IHDR";
  pallete                   = "PLTE";
  image_data                = "IDAT";
  image_trailer             = "IEND";
  transparency              = "tRNS";
  prim_chrom_wh_pt          = "cHRM";
  image_gamma               = "gAMA";
  icc_profile               = "iCCP";
  signifigant_bits          = "sBIT";
  rgb_color_space           = "sRGB";
  text_data                 = "tEXt";
  compressed_text_data      = "xTXt";
  international_text_data   = "iTXt";
  background_color          = "bKGD";
  image_histogram           = "hIST";
  physical_pixel_dimensions = "pHYs";
  suggested_pallete         = "sPLT";
  last_modified_timestamp   = "tIME";
}

switch ChunkData<ChunkType> {
  case image_header:              ImageHeader;
  case pallete:                   Pallete[...];
  case image_data:                void;
  case image_trailer:             void;
  case transparency:              TransparencyData($.image_header.color_type);
  case prim_chrom_wh_pt:          PrimaryChromaticitiesAndWhitePointData;
  case image_gamma:               GammaData;
  case icc_profile:               EmbeddedICCProfileData;
  case signifigant_bits:          SignifigantBitsData($.image_header.color_type);
  case rgb_color_space:           RGBColorSpaceData;
  case text_data:                 TextData;
  case compressed_text_data:      CompressedTextData;
  case international_text_data:   InternationalTextData;
  case background_color:          BackgroundColorData($.image_header.color_type);
  case image_histogram:           ImageHistogramData;
  case physical_pixel_dimensions: PhysicalPixelDimensionsData;
  case suggested_pallete:         SuggestedPalleteData;
  case last_modified_timestamp:   LastModifiedTimestampData;

  # If we don't recognize the chunk type, just leave it as an
  # unprocessed byte array
  # TODO: Make sure void when used as a refinement like this
  # actually does leave the data in original form
  default: void;
}

enum BitDepth: u8 {
  b1  = 1;
  b2  = 2;
  b4  = 4;
  b8  = 8;
  b16 = 16;
}

enum CompressionMethod: u8 {
  zlib = 0;
}

enum FilterMethod: u8 {
  adaptive = 0;
}

enum InterlaceMethod: u8 {
  no_interlace    = 0;
  adam7_interlace = 1;
}

struct ImageHeader {
  width: u32_be;
  height: u32_be;
  bit_depth: BitDepth;
  color_type: ColorType;
  compression_method: CompressionMethod;
  filter_method: FilterMethod;
  interlace_method: InterlaceMethod;
}

enum ColorType: u8 {
  greyscale            = 0;
  truecolor            = 2;
  indexed_color        = 3;
  greyscale_with_alpha = 4;
  truecolor_with_alpha = 6;
}

struct Pallete {
  red: u8;
  green: u8;
  blue: u8;
}

switch TransparencyData<ColorType> {
  case greyscale: GreyscaleTransparencyData;
  case truecolor: TruecolorTransparencyData;
  case indexed_color: IndexedColorTransparencyData;
  default: invalid;
}

struct GreyscaleTransparencyData {
  grey_sample: u16_be;
}

struct TruecolorTransparencyData {
  red_sample: u16_be;
  green_sample: u16_be;
  blue_sample: u16_be;
}

struct IndexedColorTransparencyData {
  alpha_values: u8[...];
}

struct PrimaryChromaticitiesAndWhitePointData {
  white_point_x: u32_be;
  white_point_y: u32_be;
  red_x: u32_be;
  red_y: u32_be;
  green_x: u32_be;
  green_y: u32_be;
  blue_x: u32_be;
  blue_y: u32_be;
}

struct GammaData {
  image_gamma: u32_be;
}

struct EmbeddedICCProfileData {
  proile_name: ascii<null>;
  compression_method: CompressionMethod;
  compressed_profile: u8[...];
}

switch SignifigantBitsData<ColorType> {
  case greyscale:            GreyscaleSignifigantBitsData;
  case truecolor:            ColorSignifigantBitsData;
  case indexed_color:        ColorSignifigantBitsData;
  case greyscale_with_alpha: GreyscaleSignifigantBitsData;
  case truecolor_with_alpha: ColorSignifigantBitsData;
  default: invalid;
}

struct GreyscaleSignifigantBitsData {
  signifigant_greyscale_bits: u8;
  signifigant_alpha_bits?($.image_header.color_type == ColorType.greysacle_with_alpha): u8;
}

struct ColorSignifigantBitsData {
  signifigant_red_bits: u8;
  signifigant_green_bits: u8;
  signifigant_blue_bits: u8;
  signifigant_alpha_bits?($.image_header.color_type == ColorType.truecolor_with_alpha): u8;
}

enum RGBRenderingIntent: u8 {
  perceptual            = 0;
  relative_colorimetric = 1;
  saturation            = 2;
  absolute_colorimetric = 3;
}

struct RGBColorSpaceData {
  rendering_intent: RGBRenderingIntent;
}

struct TextData {
  keyword: ascii<null>;
  text: ascii<...>;
}

struct CompressedTextData {
  keyword: ascii<null>;
  compression_method: u8;
  compressed_text: u8[...];
}

switch InternationalTextDataBody<bool> {
  case true: u8[...];
  case false: utf8<...>;
}

struct InternationalTextData {
  keyword: ascii<null>;
  compression_flag: bool;
  compression_method: u8;
  language_tag: ascii<null>;
  translated_keyword: u8[null];
  text: InternationalTextDataBody(@.compression_flag);
}

switch BackgroundColorData<ColorType> {
  case greyscale:            GreyscaleBackgroundData;
  case greyscale_with_alpha: GreyscaleBackgroundData;
  case truecolor:            TruecolorBackgroundData;
  case truecolor_with_alpha: TruecolorBackgroundData;
  case indexed_color:        IndexedColorBackgroundData;
  default: invalid;
}

struct GreyscaleBackgroundData {
  greyscale: u16_be;
}

struct TruecolorBackgroundData {
  red: u16_be;
  green: u16_be;
  blue: u16_be;
}

struct IndexedColorBackgroundData {
  pallete_index: u8;
}

struct ImageHistogramData {
  frequencies: u16_be[...];
}

enum PixelDimensionUnit: u8 {
  unknown = 0;
  meter   = 1;
}

struct PhysicalPixelDimensionsData {
  ppu_x: u32_be;
  ppu_y: u32_be;
  unit: PixelDimensionUnit;
}

enum SampleDepth: u8 {
  b8  = 8;
  b16 = 16;
}

struct Sample8Bit {
  red: u8;
  green: u8;
  blue: u8;
  alpha: u8;
}

struct Sample16Bit {
  red: u16_be;
  green: u16_be;
  blue: u16_be;
  alpha: u16_be;
}

switch Sample<SampleDepth> {
  case b8: Sample8Bit;
  case b16: Sample16Bit;
  default: invalid;
}

struct SuggestedPalleteData {
  pallete_name: ascii<null>;
  sample_depth: SampleDepth;
  ...Sample(@.sample_depth);
  frequencies: u16_be[...];
}

struct LastModifiedTimestampData {
  year: u16_be;
  month: u8;
  day: u8;
  hour: u8;
  minute: u8;
  second: u8;
}
